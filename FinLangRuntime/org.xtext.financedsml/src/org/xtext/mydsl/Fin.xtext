grammar org.xtext.mydsl.Fin with org.eclipse.xtext.common.Terminals

generate fin "http://www.xtext.org/mydsl/Fin"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	(element+=Element)*;
	
Element:
	Portfolio | Transaction | Action
;

Asset:
	Bond | Option | Cash
;

Transaction:
	Buy | Sell | Deposit | Withdrawal
;

// TODO: Thinking to add more actions
Action: 
	Display
;

// Use portfolio to initialise a profile
Portfolio:
	'Portfolio' '{'
		(asset+=Asset)+
	'}'
;

Bond:
	'Bond' name=ID '{'
		'faceValue' faceValue=FLOAT
		'couponRate' couponRate=FLOAT
		'maturity' maturity=INT
		'yieldRate' yieldRate=FLOAT
	'}'
;

Option:
	'Option' name=ID '{'
		'type' type=OptionType
		'underlyingPrice' underlyingPrice=FLOAT
		'strikePrice' strikePrice=FLOAT
		'timeToExpiration' timeToExpiration=FLOAT
		'riskFreeRate' riskFreeRate=FLOAT
		'volatility' volatility=FLOAT
		'dividendYield' dividendYield=FLOAT
	'}'
;

Cash:
    'Cash' '{'
    	'amount' amount=FLOAT
    '}'
;


Buy:
    'Buy' '{'
        (bond=Bond | option=Option)
    '}'
;

Sell:
    'Sell' '{'
        'assetName' name=ID
        'amount' amount=FLOAT
    '}';

Deposit:
	'Deposit' amount=FLOAT
;

Withdrawal:
	'Withdrawal' amount=FLOAT
;

Display:
	'Display' (displayType=DisplayType)
;

DisplayType:
	'Porfolio' | 'Bond' | 'Option' | 'Cash' | 'All'
;

enum OptionType:
	Call | Put
;

FLOAT returns ecore::EFloat hidden():
	INT? "." INT
;

